plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
    id 'maven-publish'
}

group = 'com.github.danielbinder'
version '1.6.4'

application {
    mainClass = 'main.Main'
}

shadowJar {
    setArchiveClassifier(null)
    dependsOn test
}

compileJava {
    dependsOn ':visualisation:npm-run-build'
    options.compilerArgs << "-Xlint:all"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }

        resources {
            srcDirs = ['visualisation/build']
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.sparkjava:spark-core:2.9.4"
    implementation "org.slf4j:slf4j-nop:2.0.6"

    testImplementation "org.junit.jupiter:junit-jupiter:5.10.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.8.1"
}

publishing {
    publications {
        pub(MavenPublication) {
            artifact shadowJar
        }
    }
}